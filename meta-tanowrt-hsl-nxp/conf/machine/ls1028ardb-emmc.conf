#
# SPDX-License-Identifier: MIT
# Copyright (c) 2021 Tano Systems LLC. All rights reserved.
#

#@TYPE: Machine
#@NAME: NXP LS1028A RDB
#@SOC: LSCH3
#@DESCRIPTION: Machine configuration for running NXP LS1028A RDB board running on eMMC internal flash with bootloader in NOR flash memory

require ${TANOWRT_HSL_BASE}/conf/machine/include/ls1028ardb-common.inc

OPENWRT_VERSION_PRODUCT:append = " (eMMC)"

#
# Bootloader
#
UBOOT_CONFIG = "sdcard-defconfig emmc-defconfig"
UBOOT_CONFIG[emmc-defconfig] = "ls1028ardb_emmc_defconfig,,u-boot-with-spl.bin"
UBOOT_CONFIG[sdcard-defconfig] = "ls1028ardb_sdcard_defconfig,,u-boot-with-spl.bin"

#
# RCW
#
RCW_BINARY = "${RCW_BINARY_EMMC_BOOT}"

#
# Machine/distro features
#
MACHINE_FEATURES += "swupdate-factory"

#
# Image
#

# Initramfs (used for factory installation image)
INITRAMFS_IMAGE = "tanowrt-image-initramfs-swu-factory"
INITRAMFS_IMAGE_NAME = "${INITRAMFS_IMAGE}-${MACHINE}"
INITRAMFS_IMAGE_BUNDLE = "0"

#
# SWUPDATE
#
SWU_BOARD = "ls1028ardb-emmc"
SWU_REVISION = "1.0"

SWU_IMAGE_MEDIA_TYPE = "eMMC"
SWU_IMAGE_MMCDEV = "/dev/mmcblk1"

# Extra dependencies for factory installation script
SWU_INSTALL_EXTRA_RDEPENDS += "parted mmc-utils"

SWU_IMAGE_ARTIFACT_uboot[fname] = "u-boot-${MACHINE}"
SWU_IMAGE_ARTIFACT_uboot[ftype] = "bin-emmc-defconfig"

#
# Factory image
#
SWU_FACTORY_WKS_FILE ?= "sdimage-ls1028ardb-swu-factory.wks.in"
SWU_FACTORY_EXTRA_DEPENDS += "\
	u-boot-startup-script:do_deploy \
	virtual/bootloader:do_deploy \
	virtual/kernel:do_deploy \
	rcw:do_deploy \
	dp-firmware-cadence:do_deploy \
	ppa:do_deploy \
"

SWU_FACTORY_EXTRA_FILES = "\
	u-boot-initial-env-emmc-defconfig.bin;u-boot-initial-env.bin \
	${RCW_BINARY};rcw.bin \
"

# These variables are used in sdimage-ls1028ardb-swu-factory.wks.in
WKS_FACTORY_IMAGE_DPFW_BINARY = "${WKS_IMAGE_DPFW_BINARY}"
WKS_FACTORY_IMAGE_PPAFW_BINARY = "${WKS_IMAGE_PPAFW_BINARY}"

WKS_FACTORY_IMAGE_UBOOT_BINARY = "u-boot-${MACHINE}.bin-sdcard-defconfig"
WKS_FACTORY_IMAGE_STARTUP_BINARY = "startup-factory-${MACHINE}.img"
WKS_FACTORY_IMAGE_UBOOT_INITIAL_ENV_BINARY = "u-boot-initial-env-${MACHINE}-sdcard-defconfig.bin"
WKS_FACTORY_IMAGE_RCW_BINARY = "rcw/ls1028ardb/R_SQPH_0x85be/rcw_1300_sdboot.bin"
WKS_FACTORY_IMAGE_KERNEL_BINARY = "fitImage-${INITRAMFS_IMAGE}-${MACHINE}.ext4"

#
# Other
#

# Do not need auto overlay resizing for eMMC
TANOWRT_ENABLE_OVERLAY_RESIZE = "0"
