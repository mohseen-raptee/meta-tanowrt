#
# SPDX-License-Identifier: MIT
#
# Rapid reverse DNS rpcd module
#
# This file Copyright (c) 2019, 2021 Tano Systems LLC. All rights reserved.
# Anton Kikin <a.kikin@tano-systems.com>
#
PR = "tano1"
DESCRIPTION = "Rapid reverse DNS rpcd module"

LICENSE = "Apache-2.0"
LIC_FILES_CHKSUM = "file://${COREBASE}/meta/files/common-licenses/Apache-2.0;md5=89aea4e17d99a7cacdbeed46a0096b10"

DEPENDS += "libubox libubus rpcd"
RDEPENDS:${PN} += "rpcd"

do_configure[depends] += "rpcd:do_populate_sysroot"

inherit cmake
inherit tanowrt-luci

LUCI_DO_POST_INSTALL_ACTIONS = "0"

SRC_URI = "${LUCI_GIT_URI};branch=${LUCI_GIT_BRANCH};protocol=${LUCI_GIT_PROTOCOL};subpath=libs/rpcd-mod-rrdns/src;destsuffix=git/"
SRCREV = "${LUCI_GIT_SRCREV}"
S = "${WORKDIR}/git"

do_install() {
	install -dm 0755 ${D}${libdir}/rpcd
	install -m 0755 ${B}/rrdns.so ${D}${libdir}/rpcd/rrdns.so
}

FILES:${PN} += "${libdir}/rpcd"

pkg_postinst:${PN}:append() {
#!/bin/sh
IPKG_INSTROOT=$D
if [ -z "${IPKG_INSTROOT}" ]; then
	# Run only on target (reload rpcd modules)
	killall -HUP rpcd 2>/dev/null
fi
}
