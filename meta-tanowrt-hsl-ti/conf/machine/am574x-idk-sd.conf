#
# SPDX-License-Identifier: MIT
# Copyright (c) 2020 Tano Systems LLC. All rights reserved.
#

#@TYPE: Machine
#@NAME: AM574x IDK EVM
#@DESCRIPTION: Machine configuration for the AM574x IDK EVM running on SD card

require conf/machine/include/am574x-idk.inc

OPENWRT_VERSION_PRODUCT:append = " (SD)"

#
# Bootloader
#
UBOOT_MACHINE = ""
UBOOT_CONFIG = "sdcard-defconfig"
UBOOT_CONFIG[sdcard-defconfig] = "am574x_idk_sdcard_defconfig,,u-boot.img"

#
# Image
#
IMAGE_FSTYPES += "wic"
IMAGE_CLASSES += "wic-sdcard-img"

TANOWRT_ENABLE_OVERLAY_RESIZE = "1"

PACKAGECONFIG:append:pn-fstools = " ovl-rootdisk-part "

# These variables are used in sdimage-ti-swu-a-b.wks.in
WKS_IMAGE_MLO_BINARY = "MLO-sdcard-defconfig"
WKS_IMAGE_UBOOT_BINARY = "u-boot-${MACHINE}.img-sdcard-defconfig"
WKS_IMAGE_UBOOT_INITIAL_ENV_BINARY = "u-boot-initial-env-${MACHINE}-sdcard-defconfig.bin"
WKS_IMAGE_STARTUP_BINARY = "startup-${MACHINE}.img"
WKS_IMAGE_KERNEL_BINARY = "fitImage-${MACHINE}.ext4"
WKS_IMAGE_ROOTFS_BINARY = "${IMAGE_BASENAME}-${MACHINE}.${ROOTFS_IMAGE_FSTYPE}"

# WIC
WKS_FILE = "sdimage-ti-swu-a-b.wks.in"
WIC_CREATE_EXTRA_ARGS = "--no-fstab-update"

WIC_DO_IMAGE_DEPENDS = "\
	${IMAGE_BASENAME}:do_image_squashfs_lzo \
	u-boot-startup-script:do_deploy \
	virtual/bootloader:do_deploy \
	virtual/kernel:do_deploy \
"
do_image_wic[depends] += "${WIC_DO_IMAGE_DEPENDS}"

#
# SWUPDATE
#
SWU_BOARD = "am574x-idk-sd"
SWU_REVISION = "1.0"

SWU_IMAGE_MEDIA_TYPE = "SD Card"
SWU_IMAGE_MMCDEV = "/dev/mmcblk0"

SWU_IMAGE_ARTIFACT_spl[fname] = "MLO-sdcard-defconfig-x${UBOOT_SPL_MULTIPLE_COUNT_SD}"
SWU_IMAGE_ARTIFACT_uboot[fname] = "u-boot-${MACHINE}"
SWU_IMAGE_ARTIFACT_uboot[ftype] = "img-sdcard-defconfig"
